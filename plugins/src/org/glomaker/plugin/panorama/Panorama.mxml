<?xml version="1.0" encoding="utf-8"?>

<!--
/////////////////////////////////////////////////////////////////////////
// Copyright (c) 2007-12 LTRI, London Metropolitan Uni. All rights reserved.
// An Open Source Release under the GPL v3 licence  (see http://www.gnu.org/licenses/).
// Authors: Tom Boyle, Nils Millahn, Musbah Sagar, Martin Agombar.
// See http://www.glomaker.org for full details
/////////////////////////////////////////////////////////////////////////
-->

<BaseComponent
	xmlns="org.glomaker.shared.component.*" 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	layout="absolute"
	horizontalScrollPolicy="off"
	verticalScrollPolicy="off"
	clipContent="true"
	backgroundColor="#f9f9f9"
	xmlns:dragdrop="org.glomaker.plugin.dragdrop.*" xmlns:editbutton="org.glomaker.shared.ui.editbutton.*" xmlns:panorama="org.glomaker.plugin.panorama.*"
>
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			
			import org.glomaker.shared.properties.BooleanProperty;
			import org.glomaker.shared.properties.FilePathArrayProperty;
			import org.glomaker.shared.properties.FilePathProperty;
			import org.glomaker.shared.properties.IComponentProperty;
			import org.glomaker.shared.properties.NumberProperty;
			
			//--------------------------------------------------
			// Protected vars
			//--------------------------------------------------
			
			// component properties
			protected var imageSource:FilePathProperty = new FilePathProperty("imageSource", "Choose an image:" ); // internal use only, not persisted
			protected var images:FilePathArrayProperty = new FilePathArrayProperty("images");
			protected var showDegrees:BooleanProperty = new BooleanProperty("showDegrees", "Web version shows degrees", true);
			
			// other
			[Bindable]
			protected var imageListData:ArrayCollection; // image 0 is pointing north
			
			[Bindable]
			protected var editMode:Boolean;
			
			[Bindable]
			protected var selectedImageSource:String;
			
			[Bindable]
			protected var showDegreeTextField:Boolean;

			/**
			 * Index into imageListData of the image that the user has clicked on.
			 * Holds the value -1 when no image is selected.
			**/
			protected var selectedIndex:int = -1;
			
			/**
			 * Used to scroll the image list after an image has been added.
			**/
			protected var scrollAfterUpdate:Boolean = false;
			
			
			//--------------------------------------------------
			// GLOMaker Component Lifecycle
			//--------------------------------------------------
			
			override protected function defineProperties():void
			{
				addSaveableProperty( images );
				addSaveableProperty( showDegrees );
				addEditableProperty( imageSource );
				addEditableProperty( showDegrees );
			}
			
			override protected function componentInitComplete():void
			{
				// data structures
				imageListData = new ArrayCollection( images.propValue );
				showDegreeTextField = showDegrees.propValue;
			}
			
			override public function editablePropertyUpdated(prop:IComponentProperty):void
			{
				super.editablePropertyUpdated(prop);
				
				if( prop == imageSource )
				{
					// this happens if the user is adding an image or changing an existing image
					// we can determine which by checking if selectedIndex is pointing to a valid value
					if( selectedIndex == -1 )
					{
						
						// scroll list to the end to make sure new image is visible
						scrollAfterUpdate = true;

						// adding a new image
						var newIndex:uint = imageListData.length;
						imageListData.addItem( prop.propValue );
						
					}else{
						// currently viewing an existing image
						// simply update the data model and the image shown
						selectedImageSource = prop.propValue;
						imageListData.setItemAt( prop.propValue, selectedIndex );
					}
				}else if( prop == showDegrees ){
					showDegreeTextField = prop.propValue;
				}
			}
			
			override public function prepareValuesForSave():void
			{
				super.prepareValuesForSave();
				images.propValue = imageListData.source.concat();
			}
			
			override public function setEditMode(value:Boolean):void
			{
				super.setEditMode(value);
				
				if (value == editMode)
					return;
				
				editMode = value;
			}
			
			override public function destroy():void
			{
				super.destroy();
			}

			
			//--------------------------------------------------
			// Protected functions
			//--------------------------------------------------
			
			protected function select( e:MouseEvent ):void
			{
				showImagePanel( Thumbnail( e.currentTarget ).listIndex );
			}
			
			protected function remove():void
			{
				if( selectedIndex == -1 )
					return;
				
				// remove from data model and deselect
				imageListData.removeItemAt( selectedIndex );
				selectedIndex = -1;
				
				// triggered from a button on the image panel
				closeImagePanel();
			}
			
			protected function setNorth():void
			{
				if( selectedIndex == -1 )
					return;
				
				// the 'north' image is always at the beginning of the array
				// we reorganise accordingly
				var newList:Array = imageListData.source.slice( selectedIndex ).concat( imageListData.source.slice( 0, selectedIndex ) );
				imageListData.source = newList;
				
				// close the popup
				closeImagePanel();
			}
			
			protected function showImagePanel( index:int ):void
			{
				selectedIndex = index;

				if( selectedIndex != -1 )
				{
					selectedImageSource = String(imageListData.getItemAt( selectedIndex ));
				}

				setNorthButton.enabled = (selectedIndex > 0); // because image 0 is already north
				
				imagePanel.visible = true;
				modalOverlay.visible = true;
			}
			
			protected function closeImagePanel():void
			{
				modalOverlay.visible = false;
				imagePanel.visible = false;
				selectedIndex = -1;
			}
			
			protected function repeatEnd():void
			{
				if( scrollAfterUpdate )
				{
					scrollAfterUpdate = false;
					imageContainer.validateNow(); // force measure
					imageContainer.horizontalScrollPosition = imageContainer.maxHorizontalScrollPosition;
				}
			}
			
		]]>
	</mx:Script>
	
	<mx:Style>
		.imageContainer{
			backgroundColor: #004890;
		}
		
		.thumbnailBox
		{
			paddingTop: 10;
			paddingBottom: 26;
			paddingLeft: 10;
			paddingRight: 10;
		}
		.popup {
			backgroundColor: #ffffff;
			borderColor: #004890;
			borderThickness: 1;
			borderStyle: solid;
			paddingLeft: 10;
			paddingRight: 10;
			paddingTop: 10;
			paddingBottom: 10;
		}
		.modalOverlay {
			backgroundColor: #000000;
		}
		.addButtonLabel {
			font-size: 9;
			font-weight: normal;
		}
		.thumbnail
		{
			backgroundColor: #ffffff;
		}
	</mx:Style>
	
	<mx:Fade id="fadeIn" duration="300" alphaFrom="0.0" alphaTo="1.0" />
	
	<mx:Glow id="glowThumb" duration="100"
			 alphaFrom="0" alphaTo="0.8"
			 blurXFrom="0.0" blurXTo="15.0" 
			 blurYFrom="0.0" blurYTo="15.0"  strength="1"
			 color="0xffffff"/>
	
	<mx:Glow id="unglowThumb" duration="100"  
			 alphaFrom="0.8" alphaTo="0"
			 blurXFrom="15.0" blurXTo="0.0"
			 blurYFrom="15.0" blurYTo="0.0"  strength="1"
			 color="0xffffff"/>
		
	<mx:Canvas
		id="imageContainer"
		styleName="imageContainer"
		height="50%"
		width="100%"
		verticalCenter="0"
		verticalScrollPolicy="off"
	>
		<mx:HBox height="100%" styleName="thumbnailBox">
			<mx:Repeater dataProvider="{imageListData}" id="imgRp" repeatEnd="repeatEnd()">
				<panorama:Thumbnail
					source="{imgRp.currentItem}"
					styleName="thumbnail"
					listIndex="{imgRp.currentIndex}"
					degrees="{(imgRp.currentIndex + 1) + ': ' + Math.round(imgRp.currentIndex*360/imageListData.length) + 'Â°'}"
					showText="{showDegreeTextField}"
					height="100%"
					width="100"
					buttonMode="true"
					click="select(event)"
					rollOverEffect="{glowThumb}"
					rollOutEffect="{unglowThumb}"
				/>
			</mx:Repeater>
		</mx:HBox>
	</mx:Canvas>
	
	<mx:Label id="addButtonLabel" showEffect="{fadeIn}" visible="{editMode}" text="Click 'Browse' below to add an image" styleName="addButtonLabel" bottom="10" horizontalCenter="0" />
	
	<mx:Canvas
		visible="false"
		id="modalOverlay"
		styleName="modalOverlay"
		alpha="0.4"
		width="100%"
		height="100%"
		/>
	
	<mx:VBox
		visible="false"
		id="imagePanel"
		styleName="popup"
		verticalScrollPolicy="off"
		horizontalScrollPolicy="off"
		width="85%"
		height="85%"
		verticalCenter="0"
		horizontalCenter="0"
		horizontalAlign="center"
		showEffect="{fadeIn}"
	>
		<mx:Image
			width="100%"
			height="100%"
			verticalAlign="middle"
			horizontalAlign="center"
			source="{selectedImageSource}"/>
		<mx:HBox>
			<mx:Button id="setNorthButton" click="setNorth()" label="Set as North" visible="{editMode}" includeInLayout="{editMode}" />
			<mx:Button click="remove()" label="Remove Image" visible="{editMode}" includeInLayout="{editMode}" />
			<mx:Spacer width="20" visible="{editMode}" includeInLayout="{editMode}" />
			<mx:Button click="closeImagePanel()" label="Close" />
		</mx:HBox>
		<mx:Label fontWeight="bold" text="Use the properties panel to change the image" visible="{editMode}" includeInLayout="{editMode}"/>
	</mx:VBox>
	
</BaseComponent>